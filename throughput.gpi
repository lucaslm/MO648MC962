reset
set terminal png
set output '/dev/null'

if (!exists("yColumn")) yColumn=2

# Plot once to store maximum value
if (exists("errorColumn")) \
  plot dataFile using 1:(column(yColumn)):(column(errorColumn)) with yerrorbars; \
else \
  plot dataFile using 1:(column(yColumn)) with points

MAX=GPVAL_X_MAX

set xrange [0:MAX+MAX*0.05]

set output outPath
#set term postscript eps dl 1.7 "Helvetica" 17
set encoding iso_8859_1
set xlabel 'Number of Senders'
set ylabel 'ThroughPut (Mbps)'
set style data linespoints

if (exists("errorColumn")) \
  plot dataFile using 1:(column(yColumn)):(column(errorColumn)) title '' with yerrorbars; \
else \
  plot dataFile using 1:(column(yColumn)) title '' with points
